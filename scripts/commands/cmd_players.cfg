// show player stats for all players
// modified by /BudSpencer (2016)


//returns string containing player's statistics
getplayerstatstr = [
	str = (format "^f1>>> ^f2Name: ^f4%1 ^f4| ^f2Country: ^f4%2 ^f4| ^f2Frags(Kills): ^f4%3 ^f4| ^f2Deaths: ^f4%4 ^f4| ^f2Accuracy: ^f4%5%% ^f4| ^f2KpD: ^f4%6% ^f4|" (getname $arg1) (getcountry (getip $arg1)) (getfrags $arg1) (getdeaths $arg1) (getaccuracy $arg1) (getkpd $arg1))	
	curr_mode = (modetostr (getmode))
	// current gamemode is team mode
	if (= (isinlist $TEAMMODES $curr_mode) 1) [
		str = (concatword $str (format " ^f2Teamkills: ^f4%1 ^f4|" (getteamkills $arg1)))
		
		// current gamemode has flags
		if (= (isinlist $FLAGMODES $curr_mode) 1) [
			str = (concatword $str (format " ^f2Scored Flags: ^f4%1 ^f4|" (getflags $arg1) ))
			// str = (concatword $str (format " ^f7Returned flags: %1" (getretflags $arg1)))  // not implemented yet
		]
	]
	str = (concatword $str (format " ^f2Rank: ^f4%1^f7 ^f4|" (getrank $arg1))) 
	str = (concatword $str (format " ^f2Time: ^f4%1^f7 ^f1<<<" (formatmillis "%hh %mm %ss" (getonline $arg1))))
	result $str
]

cmd_players = [
		if (strcmp $arg2 "") [
			rank = ""
			a = (sortlist (allplayers))
			looplist cn $a [	
				rank = (concat $rank (getrank $cn))
			]
			a = (sorttwolists $rank $a)
			
			looplist cn $a [
				s = (getplayerstatstr $cn)
				if (= $cn $arg1) [str = (concat $s "^f0*")]
				pm $arg1 $s
			]
		] [
			pm $arg1 (getplayerstatstr $arg2)
		]
	
]

registercommand "players" cmd_players 1 "" "players ^f2This command shows game statistics for all players."
